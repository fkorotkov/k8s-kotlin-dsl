apply plugin: "com.jfrog.bintray"
apply plugin: "kotlin"
apply plugin: "maven-publish"

ext {
    current_k8s_dsl_version = '0.9'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath libraries['com.jfrog.bintray.gradle:gradle-bintray-plugin']
    }
}

bintray {
    user = "fkorotkov"
    key  = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = 'k8s-kotlin-dsl'
        userOrg = 'fkorotkov'
        licenses = ['MIT']
        vcsUrl = 'git@github.com:fkorotkov/k8s-kotlin-dsl.git'
        version {
            name = current_k8s_dsl_version
            desc = 'Kotlin DSL for Kubernetes'
            released  = new Date()
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.fkorotkov'
            artifactId 'kubernetes-dsl'
            version current_k8s_dsl_version
            from components.java
            artifact sourceJar
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

sourceSets {
    main {
        kotlin {
            srcDir 'src/main/kotlin-gen'
        }
    }
}

dependencies {
    compile libraries['org.jetbrains.kotlin:kotlin-stdlib']
    compile libraries['org.jetbrains.kotlin:kotlin-reflect']

    compile libraries['io.fabric8:kubernetes-model']

    testCompile libraries['junit:junit']
    testCompile libraries['org.jetbrains.kotlin:kotlin-test-junit']
}