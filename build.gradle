buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.fkorotkov:gradle-libraries-plugin:0.1'
    }
}

apply plugin: 'com.fkorotkov.libraries'

updateLibraries.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['dev', 'eap', 'M1'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('dev version')
            }
        }
    }
}

subprojects {
    buildscript {
        repositories {
            maven { setUrl("https://repo.gradle.org/gradle/repo") }
        }

        dependencies {
            classpath libraries['org.jetbrains.kotlin:kotlin-gradle-plugin']
        }
    }

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { setUrl("https://repo.gradle.org/gradle/repo") }
    }

    apply plugin: 'com.fkorotkov.libraries'
    apply plugin: "java"
    apply plugin: "jacoco"

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }

    jacocoTestReport {
        group = "Reporting"
        reports {
            xml.enabled true
            csv.enabled false
        }
    }
}