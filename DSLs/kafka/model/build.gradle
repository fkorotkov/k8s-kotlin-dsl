apply plugin: "maven-publish"
apply plugin: "kotlin"
apply plugin: "jsonschema2pojo"
apply plugin: "com.jfrog.bintray"

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { setUrl("https://dl.bintray.com/fkorotkov/maven") }
    }

    dependencies {
        classpath libraries['com.jfrog.bintray.gradle:gradle-bintray-plugin']
        classpath libraries['org.jsonschema2pojo:jsonschema2pojo-gradle-plugin']
        classpath "com.fkorotkov:k8s-kafka-annotator:1.1"
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java-gen'
        }
    }
}

bintray {
    user = "fkorotkov"
    key  = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = 'k8s-kafka-model'
        userOrg = 'fkorotkov'
        licenses = ['MIT']
        vcsUrl = 'git@github.com:fkorotkov/k8s-kotlin-dsl.git'
        version {
            name = current_k8s_dsl_version
            desc = 'Kafka Kubernetes Model'
            released  = new Date()
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.fkorotkov'
            artifactId 'k8s-kafka-model'
            version current_k8s_dsl_version
            from components.java
        }
    }
}

jsonSchema2Pojo {
    source = files("${projectDir}/src/main/resources/schema.json")
    targetDirectory = file("${projectDir}/src/main/java-gen/")
    targetPackage = "com.fkorotkov.kubernetes.kafka"
    includeConstructors = true
    includeJsr303Annotations = false
    includeToString = false
    includeHashcodeAndEquals = false
    customAnnotator = "io.fabric8.kafka.KafkaTypeAnnotator"
}

dependencies {
    annotationProcessor libraries['io.sundr:builder-annotations']
    annotationProcessor libraries['io.sundr:transform-annotations']

    compile libraries['com.fasterxml.jackson.module:jackson-module-jaxb-annotations']
    compile project(":DSLs:kafka:annotator")
    compile libraries['io.fabric8:kubernetes-client']
    compile libraries['io.fabric8:kubernetes-model']
}